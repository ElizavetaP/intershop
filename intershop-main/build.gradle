plugins {
    id 'org.openapi.generator'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    implementation 'io.r2dbc:r2dbc-h2'
    runtimeOnly 'com.h2database:h2'

    implementation 'org.springframework.boot:spring-boot-starter-data-redis-reactive'

    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.15'

    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
}

// Генерация клиента для платежного сервиса
openApiGenerate {
    generatorName.set("java")
    inputSpec.set("$rootDir/api-specs/payment-api.yaml")
    outputDir.set("$buildDir/generated")
    apiPackage.set("ru.practicum.intershop.payment.client.api")
    modelPackage.set("ru.practicum.intershop.payment.client.model")
    configOptions.set([
        library: "webclient",
        reactive: "true",
        useSpringBoot3: "true",
        dateLibrary: "java8"
    ])
}

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/src/main/java"
        }
    }
}

// Генерируем код перед компиляцией
compileJava.dependsOn tasks.openApiGenerate

jar {
    archiveBaseName = 'intershop-main'
    archiveVersion = '1.0.0'
}

bootJar {
    archiveBaseName = 'intershop-main'
    archiveVersion = '1.0.0'
}
