plugins {
    id 'org.openapi.generator'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // Spring Security для OAuth2 Resource Server
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'

    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.15'

    testImplementation 'org.springframework.security:spring-security-test'
}

// Генерация серверного кода
openApiGenerate {
    generatorName.set("spring")
    inputSpec.set("$rootDir/api-specs/payment-api.yaml")
    outputDir.set("$buildDir/generated")
    apiPackage.set("ru.practicum.payment.api")
    modelPackage.set("ru.practicum.payment.model")
    configOptions.set([
        library: "spring-boot",
        reactive: "true",
        useSpringBoot3: "true",
        interfaceOnly: "true",
        dateLibrary: "java8",
        useTags: "true"
    ])
}

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/src/main/java"
        }
    }
}

// Генерируем код перед компиляцией
compileJava.dependsOn tasks.openApiGenerate

jar {
    archiveBaseName = 'payment-service'
    archiveVersion = '1.0.0'
}

bootJar {
    archiveBaseName = 'payment-service'
    archiveVersion = '1.0.0'
}
